// `cogbox rules` - resolve --name to a config/runtime path pair, then
// hand the rest of argv off to the existing rules-module dispatch.

const std = @import("std");
const util = @import("../util.zig");
const parse = @import("../parse.zig");
const help = @import("../help.zig");
const exit_codes = @import("../exit.zig");
const paths = @import("../paths.zig");
const rules_module = @import("rules_module");

pub fn run(
	allocator: std.mem.Allocator,
	io: std.Io,
	p: *const paths.Paths,
	argv: []const []const u8,
) !void {
	// Pull out -n/--name and -h/--help and pass everything else to the
	// rules dispatch verbatim. We can't use parse.parse here because we
	// don't know the rules module's flag set; we only need to know the
	// instance.
	var name: ?[]const u8 = null;
	var rest: std.ArrayList([]const u8) = .empty;
	defer rest.deinit(allocator);

	var i: usize = 0;
	while (i < argv.len) : (i += 1) {
		const a = argv[i];
		if (std.mem.eql(u8, a, "--help") or std.mem.eql(u8, a, "-h")) {
			try help.print(io, help.RULES);
			return;
		}
		if (std.mem.eql(u8, a, "--name") or std.mem.eql(u8, a, "-n")) {
			i += 1;
			if (i >= argv.len) {
				util.die(allocator, io, "rules", exit_codes.usage, "{s} requires a value", .{a});
			}
			name = argv[i];
			continue;
		}
		if (std.mem.startsWith(u8, a, "--name=")) {
			name = a[7..];
			continue;
		}
		if (std.mem.startsWith(u8, a, "-n=")) {
			name = a[3..];
			continue;
		}
		try rest.append(allocator, a);
	}

	if (name) |n| {
		if (std.mem.eql(u8, n, "default")) {
			util.die(allocator, io, "rules", exit_codes.dataerr, "'default' is reserved. Omit --name to use the default instance.", .{});
		}
		if (!parse.isValidName(n)) {
			util.die(allocator, io, "rules", exit_codes.dataerr, "instance name must start with a letter and contain only [a-zA-Z0-9-] (max 64 chars)", .{});
		}
	}

	const inst_cfg = try paths.instanceConfigDir(allocator, p, name);
	defer allocator.free(inst_cfg);
	const cfg_path = try std.fs.path.join(allocator, &.{ inst_cfg, "config.json" });
	defer allocator.free(cfg_path);
	const inst_runtime = try paths.instanceRuntime(allocator, p, name);
	defer allocator.free(inst_runtime);

	const cwd = std.Io.Dir.cwd();
	cwd.access(io, cfg_path, .{}) catch {
		util.die(allocator, io, "rules", exit_codes.noinput, "no config found at {s}", .{cfg_path});
	};

	try rules_module.dispatch(allocator, io, cfg_path, inst_runtime, rest.items);
}
